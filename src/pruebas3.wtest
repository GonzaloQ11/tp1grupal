import Base.*
import hechizos.*
import lucha.*
import habilidadesAvanzadas.*

describe "Tests de Habilidades Avanzadas" {

	const espectroMalefico = new Logos("espectro malefico")
	const rolando = new Personaje(espectroMalefico)
	const armaduraBasica = new Armadura(2, sinRefuerzo)
	const collarDivinoDe5Perlas = new CollarDivino(5)
	const mascaraOscura = new MascaraOscura(1, 4)
	const espada = new ArmaAfilada()

	fixture {
		rolando.agregarArtefacto(espada)
		rolando.agregarArtefacto(collarDivinoDe5Perlas)
		rolando.agregarArtefacto(mascaraOscura)
		rolando.agregarArtefacto(armaduraBasica)
		rolando.agregarArtefacto(espejo)
	}

	method unidadesDeLuchaConNuevoRefuerzo(valorEsperado, refuerzo) {
		armaduraBasica.refuerzo(refuerzo)
		assert.equals(valorEsperado, armaduraBasica.unidadesDeLucha(rolando))
	}

	test "Rolando esta cargado" {
		assert.that(rolando.estaCargado())
	}

	test "Rolando sin armadura no esta cargado" {
		rolando.eliminarArtefacto(armaduraBasica)
		assert.notThat(rolando.estaCargado())
	}

	test "El valor de lucha de Rolando es de 20" {
		assert.equals(20, rolando.valorDeLucha())
	}

///////////////
	test "La habilidad de lucha de la armadura de Rolando con una bendicion de refuerzo es de 58" {
		self.unidadesDeLuchaConNuevoRefuerzo(58, new Bendicion())
	}

	test "La habilidad de lucha de la armadura de Rolando con una Cota de Malla de refuerzo es de 3" {
		self.unidadesDeLuchaConNuevoRefuerzo(3, new CotaDeMalla(1))
	}

	test "La habilidad de lucha de la armadura de Rolando con un hechiso basico de refuerzo es de 12" {
		self.unidadesDeLuchaConNuevoRefuerzo(12, new RefuerzoHechizo(hechizoBasico))
	}

	test "El valor de lucha de Rolando con una armadura con hechizo de bendicion es de 19" {
		self.unidadesDeLuchaConNuevoRefuerzo(19, new RefuerzoHechizo(espectroMalefico))
	}

/////////////////////
	test "El valor de lucha de Rolando al eliminar todos los Artefactos es de 1" {
		rolando.eliminarTodosLosArtefactos()
		assert.equals(1, rolando.valorDeLucha())
	}

	test "El valor de hechiceria de Rolando al cambiar su hechizo preferido 
	por un libro de hechizos que tiene  un hechizo Logos con nombre espectro malefico 
	y un hechizo basico es de 56 " {
		libroDeHechizos.agregarHechizo(espectroMalefico)
		libroDeHechizos.agregarHechizo(hechizoBasico)
		rolando.hechizoPreferido(libroDeHechizos)
		assert.equals(56, rolando.nivelHechiceria())
	}

	test "El valor de lucha de Rolando al agregar una armadura con valor de lucha base de 5 con una Cota de Malla de calidad 6 es de 37" {
		const cotaDeMallaCalidad6 = new CotaDeMalla(6)
		const armaduraExtra = new Armadura(5, cotaDeMallaCalidad6)
		rolando.agregarArtefacto(armaduraExtra)
		assert.equals(37, rolando.valorDeLucha())
	}

}

